import "../imports/json"
import "../imports/markdown"

make object config, documentation_config = {}
make string output_folder = ""

define void compile_documentation() {
    // fetch configuration
    make file = read("docato-config.json")
    set config = json_parse(file)

    do validate_config("config", ["output", "page_header", "header", "footer", "index_footer", "documentation_config"], config)
    
    set output_folder = config->output
    set documentation_config = config->documentation_config

    do validate_config("documentation_config", ["title", "index_page", "favicon", "styles", "scripts", "pages", "variables", "output_name"], documentation_config)    

    // remove output folder
    do deleteDirectory(output_folder, true) catch sayln("Could not delete old output folder")
    // recreate empty output folder
    do createDirectory(output_folder)

    // create output folder
    set output_folder = `{output_folder}/{documentation_config->output_name}`
    do sayln(`Creating output folder: {output_folder}`)
    do createDirectory(output_folder)

    // compile index page
    do compilePage(documentation_config->index_page, `{output_folder}/index.html`, "index", -1)

    // compile pages
    do map(documentation_config->pages, void (page, index) => {
        do compilePage(page->source, `{output_folder}/{page->id}.html`, page->title, index)
    })
}

define void validate_config(string config_name, array fields, object config) {
    if config->(field) == null then throw(`Field not found on {config_name}: {field}`) for field in fields
}

define void compilePage(string page_source, string page_output, string name, int page_index) {
    if exists(page_source) then {
        const contents = convertDocatoVariables(markdown_to_html(read(page_source)), name, page_index)
        const page_header = convertDocatoVariables(markdown_to_html(read(config->page_header)), name, page_index)
        const header = convertDocatoVariables(read(config->header), name, page_index)
        const footer = convertDocatoVariables(markdown_to_html(read(config->footer)), name, page_index)
        
        do sayln(`Compiling page: {page_source} -> {page_output}`)

        const output = `<!DOCTYPE html><html><head>{header}</head><body>{page_header}<main>{contents}</main>{footer}</body></html>`

        do write(page_output, output)
    } else {
        do throw(`Page source not found: {page_source}`)
    }
}


define string convertDocatoVariables(string content, string page, int page_index) {
    set content = stringReplace(content, "{$main_title}", documentation_config->title)
    set content = stringReplace(content, "{$title}", page)
    set page_index++ // 1-based index

    if page_index == 0 then {
        set content = stringReplace(content, "{$previous_page}", "")
        set content = stringReplace(content, "{#previous}", "")
    } else if page_index == 1 then {
        set content = stringReplace(content, "{$previous_page}", documentation_config->index_title)
        set content = stringReplace(content, "{#previous}", `<a href="index.html">{documentation_config->index_title}</a>`)
    } else {
        set content = stringReplace(content, "{$previous_page}", documentation_config->pages#(page_index - 2)->title)
        set content = stringReplace(content, "{#previous}", `<a href="{documentation_config->pages#(page_index - 2)->id}.html">{documentation_config->pages#(page_index - 2)->title}</a>`)
    }

    if page_index == (int)documentation_config->pages then {
        set content = stringReplace(content, "{$next_page}", "")
        set content = stringReplace(content, "{#next}", "")
    } else {
        set content = stringReplace(content, "{$next_page}", documentation_config->pages#(page_index)->title)
        set content = stringReplace(content, "{#next}", `<a href="{documentation_config->pages#(page_index)->id}.html">{documentation_config->pages#(page_index)->title}</a>`)
    }

    set content = stringReplace(content, "{#index}", `<a href="index.html">{documentation_config->index_title}</a>`)
    set content = stringReplace(content, `\{#{p->id}}`, `<a href="{p->id}.html">{p->title}</a>`) for p in documentation_config->pages


    return content
}